grammar org.xtext.example.mydsl.Mymix with org.eclipse.xtext.common.Terminals

generate mymix "http://www.xtext.org/example/mydsl/Mymix"

Machine:
	(procedures+=Procedure)*  //La machine dispose de plusieurs procedures
;
	
	
Procedure:
	'procedure' name=ID   	//Nom de la procédure
	(body+=Operation)+		//Contenu de procédure a une ou plusieurs procédures
	result=Result			//Resultat retourné par la procédure
;

Operation:
	Check | Wait | TakeStuff	//3 types d'opérations : 
								//check (pour tester la quantité de l'échantillon)
								//Wait (pour l'attente)
								//TakeStuf (pour prelever un échantillon)
;

TakeStuff:
	op=Take ( body=PutSome | body=PutAll )  // les deux méthodes PutAll et PutSome pour injecter l'échantillion prelevé
;

Take:
	'take' quantity=Quantity 'from' cup=Cup ('filter' filter=Filter)? 	
;

PutSome:
	(wait+=Wait? put+=Put)+
;

PutAll:
	'to' cup=Cup ('filter' filter=Filter)?
;

Put:
	'put' quantity=Quantity 'to' cup=Cup ('filter' filter=Filter)? 
;

Check:
	'check' cup=Cup ('forQuantity' quantity=Quantity)? 
;

Wait:
	'wait' time=Time
;

Result:
	'result' cup=Cup
;

Filter:
	filter='A' | filter='B'
;

Cup:
	'cup'? cup=INT
;

Quantity:
	quantity=INT 'mL'?
;

Time:
	time=INT 'sec'?
;